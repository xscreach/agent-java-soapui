buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.8.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
    }
}

description = 'EPAM Report portal. SoapUI client API'


allprojects {
    apply plugin: 'java'
    group = 'com.epam.reportportal'
}

project.ext.releaseMode = project.hasProperty('releaseMode')

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'net.researchgate.release'
    apply plugin: 'com.jfrog.bintray'


//    apply from: "${rootProject.projectDir}/quality.gradle"
    apply from: "${rootProject.projectDir}/release.gradle"

    sourceCompatibility = 11
    targetCompatibility = 11


    repositories {
        jcenter()
        releaseMode ? maven { url "http://dl.bintray.com/epam/reportportal" } : maven { url "https://jitpack.io" }
        maven { url "https://www.soapui.org/repository/maven2" }
        mavenCentral()
    }

    publishing {
        repositories {
            maven {
                url mavenRepoUrl
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            }
        }
    }

    //noinspection GroovyAssignabilityCheck
    task zip(type: Zip, overwrite: true) {
        def path = "${project.projectDir}/src/main/resources/"
        archiveClassifier.set('full')
        from shadowJar.outputs.files
        from(path, {
            include 'soapui-log4j.xml'
            into('/bin')
        })
        from(path, {
            include 'reportportal-listeners.xml'
            into('/bin/listeners')
        })
    }
    build.dependsOn zip
}
